import Toybox.Lang;
import Toybox.WatchUi;

/*
 * Menu item for Switch elements of type "Player".
 * Displays the current state as icons (play/pause).
 * When selected, opens a dedicated full-screen view with controls 
 * for "Play"/"Pause", "Next" and "Previous".
 */
class PlayerMenuItem extends SwitchMenuItem {

    // Returns true if the given widget matches the type handled by this menu item.
    public static function isMyType( sitemapWidget as SitemapWidget ) as Boolean {
        return 
            sitemapWidget instanceof SitemapSwitch
            && sitemapWidget.getSwitchItem().getType().equals( "Player" );
    }

    // The full-screen view is instantiated only when the
    // menu item is selected
    private var _playerView as PlayerView?;

    // Constructor
    // Initializes the BaseCommandRequest used for changing the state,
    // the Drawable for the displayed state and the base class
    public function initialize( 
        sitemapSwitch as SitemapSwitch,
        parent as BasePageMenu,
        processingMode as BasePageMenu.ProcessingMode
    ) {
        SwitchMenuItem.initialize( {
                :sitemapWidget => sitemapSwitch,
                :isActionable => true,
                :parent => parent,
                :processingMode => processingMode
            }
        );

        onStateChanged();
    }

    // Called by the delegate when the view is exited
    public function onReturn() as Void {
        _playerView = null;
    }

    // When the menu item is selected, either the full-screen view is initialized
    // and pushed onto the view stack, or, when in Wi-Fi mode, a command menu is
    // shown to let the user select one of the four available commands.
    public function onSelect() as Boolean {
        // First we see if the base class handles the event ...
        if( ! CommandMenuItem.onSelect() ) {
            // ... if not, and we do have a command request, then we
            // initialize a new full-screen view and display it
            if( hasCommandsEnabled() ) {
                var sitemapSwitch = getSitemapSwitch();
                if( sitemapSwitch.getSwitchItem().hasState() ) {
                    _playerView = new PlayerView( sitemapSwitch );
                    ViewStack.pushView(
                        _playerView,
                        new PlayerViewDelegate( self ),
                        WatchUi.SLIDE_LEFT
                    );
                } else {
                    CommandMenuHandler.showCommandSelection( 
                        sitemapSwitch.getLabel(),
                        [ ["Play", SwitchItem.ITEM_STATE_PLAY],
                          ["Pause", SwitchItem.ITEM_STATE_PAUSE],
                          ["Next", SwitchItem.ITEM_COMMAND_NEXT],
                          ["Previous", SwitchItem.ITEM_COMMAND_PREVIOUS] ],
                        self 
                    );
                }
            }
        }
        return true;
    }

    // The delegate uses this function to send play/pause commands
    // depending on the current state
    public function sendPlayPause() as Void {
        sendCommand(
            getSitemapSwitch().getSwitchItem().getState().equals( SwitchItem.ITEM_STATE_PLAY )
            ? SwitchItem.ITEM_STATE_PAUSE
            : SwitchItem.ITEM_STATE_PLAY
        );
    }

    /*
    * This function is used by both the constructor and updateWidget()
    * to refresh the displayed state.
    *
    * For PLAY and PAUSE, it uses PlayerMenuItemStateBitmap as the
    * state drawable of the base class.
    *
    * For all other states (typically NO_STATE), the state is shown
    * using the base class’s responsive state text.
    */
    public function onStateChanged() as Void {
        var sitemapSwitch = getSitemapSwitch();

        // If the view is currently open, we update it as well   
        if( _playerView != null ) {
            _playerView.updateWidget( sitemapSwitch );
        }

        var currentStateDrawable = getStateDrawable();
        var currentState = sitemapSwitch.getSwitchItem().getState();

        // This expression returns
        // true for PLAY
        // false for PAUSE
        // a String for any other state        
        var currentStateParsed = 
            currentState.equals( SwitchItem.ITEM_STATE_PLAY )
            ? true
            : currentState.equals( SwitchItem.ITEM_STATE_PAUSE )
              ? false
              : sitemapSwitch.getDisplayState();

        // For PLAY/PAUSE ...
        if( currentStateParsed instanceof Boolean ) {
            // ... we either update the existing Drawable or create a new one ...
            if( currentStateDrawable instanceof PlayerMenuItemStateBitmap ) {
                currentStateDrawable.setPlaying( currentStateParsed );
            } else {
                setStateDrawable( new PlayerMenuItemStateBitmap( currentStateParsed ) );
            }
            // ... and do not display a text.
            setStateTextResponsive( null );
        } else if( currentStateParsed instanceof String ) {
            // And for text we set the Drawable to null and instead
            // show the text
            setStateDrawable( null );
            setStateTextResponsive( currentStateParsed );
        }
    }

}